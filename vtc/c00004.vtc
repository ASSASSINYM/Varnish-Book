varnishtest "Purge objects after hash_data(cookie)"

server s1 {
   rxreq
   expect req.url == "/cookie.php"
   txresp
   rxreq
   expect req.url == "/cookie.php"
   txresp
   rxreq
   expect req.url == "/article.html"
   txresp
   rxreq
   expect req.url == "/cookie.php"
   txresp
   rxreq
   expect req.url == "/article.html"
   txresp
} -start

varnish v1 -vcl+backend {
   sub vcl_recv{
     if (req.method == "PURGE") {
        return (purge);
     }
     else if (req.http.Cookie){
        return (hash);
     }
   }
   sub vcl_hash{
      hash_data(req.http.Cookie);
   }
} -start

client c1 {
   txreq -url "/cookie.php" -hdr "Cookie: user: Alice"
   rxresp
   expect resp.http.X-Varnish == "1001"
   txreq -url "/cookie.php" -hdr "Cookie: user: Bob"
   rxresp
   expect resp.http.X-Varnish == "1003"
   txreq -url "/cookie.php" -hdr "Cookie: user: Alice"
   rxresp
   expect resp.http.X-Varnish == "1005 1002"
   txreq -url "/article.html" -hdr "Cookie: user: Alice"
   rxresp
   expect resp.http.X-Varnish == "1006"
   txreq -url "/article.html" -hdr "Cookie: user: Alice"
   rxresp
   expect resp.http.X-Varnish == "1008 1007"
} -run

varnish v1 -expect n_object == 3

client c1 {
   txreq -req PURGE -url "/cookie.php" -hdr "Cookie: user: Alice"
   rxresp
} -run

varnish v1 -expect n_object == 2

client c1 {
   txreq -url "/cookie.php" -hdr "Cookie: user: Alice"
   rxresp
   expect resp.http.X-Varnish == "1012"
   txreq -url "/article.html" -hdr "Cookie: user: Bob"
   rxresp
   expect resp.http.X-Varnish == "1014"
} -run

varnish v1 -expect n_object == 4
