varnishtest "Test Vary functionality"

server s1 {
        # Backend VXID=1002
        rxreq
        expect req.url == "/same-url"
        expect req.http.foobar == "1"
        txresp -hdr "Vary: Foobar" -hdr "Snafu: 1" -body "1111\n"

        # Backend VXID=1004
        rxreq
        expect req.url == "/same-url"
        expect req.http.foobar == "2"
        txresp -hdr "Vary: Foobar" -hdr "Snafu: 2" -body "2222\n"
} -start

varnish v1 -vcl+backend {} -start

client c1 {
        txreq -url "/same-url" -hdr "Foobar: 1"
        rxresp
        expect resp.status == 200
        # First client request with VXID=1001
        # Request misses. Creates backend request with VXID=1002
        expect resp.http.X-Varnish == "1001"
        expect resp.http.snafu == "1"
        expect resp.body == "1111\n"

        txreq -url "/same-url" -hdr "Foobar: 2"
        rxresp
        expect resp.status == 200
        # Client request with VXID=1003
        # Request misses. Creates backend request with VXID=1004
        expect resp.http.X-Varnish == "1003"
        expect resp.http.snafu == "2"
        expect resp.body == "2222\n"

        txreq -url "/same-url" -hdr "Foobar: 1"
        rxresp
        expect resp.status == 200
        # Client request with VXID=1005
        # Request hits cached object created in VXID=1002
        expect resp.http.X-Varnish == "1005 1002"
        expect resp.http.snafu == "1"
        expect resp.body == "1111\n"
} -run
